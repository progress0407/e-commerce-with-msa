import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyVersion"
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.spring' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.jpa' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.allopen' version "$kotlinVersion"
}

allprojects {
    group = 'msa-with-ddd'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = JavaVersion.VERSION_17.toString()
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'org.jetbrains.kotlin.plugin.allopen'

    bootJar.enabled = true
    jar.enabled = true

    dependencies {
        // Kotlin
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'io.github.microutils:kotlin-logging:3.0.5'

        // Kotlin for Test
        testImplementation "io.kotest:kotest-runner-junit5:$junitVersion"
        testFixturesImplementation "io.kotest:kotest-runner-junit5:$junitVersion"
        testImplementation "io.kotest:kotest-assertions-core:$junitVersion"
        testFixturesImplementation "io.kotest:kotest-assertions-core:$junitVersion"

        // Mock for kotlin
        testImplementation 'io.mockk:mockk:1.13.8'
        testImplementation 'com.ninja-squad:springmockk:4.0.2'

        implementation 'mysql:mysql-connector-java:8.0.33'

        // Actuator
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        // Yaml (Configuration Auto Complete)
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        // Validate
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    ext {
        set('springCloudVersion', "2023.0.0")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.test {
        useJUnitPlatform()
    }

    allOpen {
        annotation("jakarta.persistence.Entity")
        annotation("jakarta.persistence.Embeddable")
        annotation("jakarta.persistence.MappedSuperclass")
    }
}

/*
subprojects { project ->
    if (project.name in [':user', ':item', ':order']) {
        apply from: rootProject.file('module-dependencies/micro-service.gradle')
        dependencies {
            implementation project(':common')
        }
    }
}
*/

subprojects { project ->

//    println "## project = $project.name" // for debug
    if (!(project.name in ['common', 'eureka', 'api-gateway'])) {
//        println "## (if inner clause) project = $project.name" // for debug
        apply from: rootProject.file('module-dependencies/micro-service.gradle')

        dependencies {
//            implementation project(':common')
//            testImplementation testFixtures(project(':common'))
        }
    }
}


project(':eureka') {
    dependencies {
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'

        implementation project(':common:general')
        testImplementation testFixtures(project(':common:general'))
    }
}

project(':api-gateway') {
    dependencies {
        // Router
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

        // Service Registry Client
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.0.0'

        // Gson
        implementation 'com.google.code.gson:gson:2.10'

        testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

        implementation project(':common:rest-client')
        implementation project(':common:general')
        implementation project(':common:security')
//        testImplementation testFixtures(project(':common:general'))

        // E2E test
        testImplementation 'io.rest-assured:rest-assured'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':user') {
    apply from: rootProject.file('module-dependencies/micro-service.gradle')
    dependencies {
        implementation project(':common:general')
        implementation project(':common:security')
        implementation project(':common:rest-client')
        testImplementation testFixtures(project(':common:general'))
    }
}

project(':coupon') {
    apply from: rootProject.file('module-dependencies/micro-service.gradle')
    dependencies {

        implementation project(':common:general')
        testImplementation testFixtures(project(':common:general'))

        implementation project(':common:rest-client')
        testImplementation testFixtures(project(':common:rest-client'))
    }
}

project(':item') {
    apply from: rootProject.file('module-dependencies/micro-service.gradle')
    dependencies {
        implementation project(':common:general')
        implementation project(':common:rest-client')
        implementation project(':common:event')
        testImplementation testFixtures(project(':common:general'))
    }
}

project(':order') {
    apply from: rootProject.file('module-dependencies/micro-service.gradle')
    dependencies {
        implementation project(':common:general')
        implementation project(':common:rest-client')
        implementation project(':common:event')
        testImplementation testFixtures(project(':common:general'))
    }
}

project(':common:general') {
    bootJar.enabled = false
    jar.enabled = true

    apply from: rootProject.file('module-dependencies/micro-service.gradle')

    dependencies {
        testFixturesImplementation 'org.springframework.boot:spring-boot-starter-web'
        testFixturesImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        testFixturesImplementation 'org.springframework.boot:spring-boot-starter-test'
        testFixturesImplementation 'io.rest-assured:rest-assured'
    }
}

project(':common:security') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        // Spring Security core for BCrypt
        implementation 'org.springframework.security:spring-security-core'

        // jjwt
        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
        implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    }

}